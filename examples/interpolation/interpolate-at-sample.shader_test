# GLSL 4.40 changed the relevant language in Section 8.13.2 (Interpolation
# Functions) to:
#
#    "Component selection operators (e.g., .xy) may be used when specifying
#     interpolant."
#
# Furthermore, GLSL 4.60 clarified that
#
#    "(...) interpolant must be an l-value from an in declaration; this can
#     include a variable, a block or structure member, an array element, or
#     some combination of these."
#
# which means array subscripts on vectors should also be allowed.
#

[vertex shader]
#version 440

layout(location = 0) in vec4 piglit_vertex;

layout(location = 0) out vec3 a;

void main()
{
   gl_Position = piglit_vertex;

   a = piglit_vertex.xyz;
}

[fragment shader]
#version 440

layout(location = 0) in vec3 a;

layout(std140, push_constant) uniform block {
        int u_idx;
};

layout(location = 0) out vec4 color;

void main()
{
   /* There is no multi-sampling, so these should all be the same. */
   vec2 delta1 = a.yx - interpolateAtSample(a.yx, 0);
   float delta2 = a[2] - interpolateAtSample(a[2], 0);
   float delta3 = a[u_idx] - interpolateAtSample(a[u_idx], 0);

   if (delta1 != vec2(0.0)) {
      color = vec4(0.1, delta1.x + 0.5, delta1.y + 0.5, 0.0);
   } else if (delta2 != 0.0) {
      color = vec4(0.2, delta2 + 0.5, 0.0, 0.0);
   } else if (delta3 != 0.0) {
      color = vec4(0.3, delta3 + 0.5, 0.0, 0.0);
   } else {
      color = vec4(0.0, 1.0, 0.0, 1.0);
   }
}

[test]
clear color 0.0 0.0 0.0 0.0
clear

uniform int 0 0
draw rect -1 -1 1 2

uniform int 0 1
draw rect 0 -1 1 2

probe all rgba 0.0 1.0 0.0 1.0
